import java.util.regex.Pattern

subprojects {
    apply plugin: 'com.diffplug.spotless'

    spotless {
        // optional: limit format enforcement to just the files changed by this feature branch
        //ratchetFrom 'origin/master'

        /*format 'misc', {
    // define the files to apply `misc` to
    target '*.gradle', '*.md', '.gitignore'

    // define the steps to apply to those files
    trimTrailingWhitespace()
    indentWithSpaces() // or spaces. Takes an integer argument if you don't like 4
    endWithNewline()
  }*/

        java {
            //target '**/ConfigurationReader.java'
            targetExclude '**/build/generated/**/*'

            googleJavaFormat('1.8')

            bumpThisNumberIfACustomStepChanges(2)
            custom("errorOnWildcard", {
                def matcher = Pattern.compile("^(.*?)\\.\\*;\$", Pattern.MULTILINE).matcher(it)
                if (matcher.find()) {
                    throw new WildcardError(matcher.group())
                }
                return it
            })

            // make sure every file has the following copyright header.
            licenseHeader '''/*
 * Copyright $YEAR interactive instruments GmbH
 *
 * This Source Code Form is subject to the terms of the Mozilla Public
 * License, v. 2.0. If a copy of the MPL was not distributed with this
 * file, You can obtain one at http://mozilla.org/MPL/2.0/.
 */
'''
        }
    }
}

class WildcardError extends AssertionError {
    WildcardError(String var1) {
        super((Object) "Wildcard imports are not allowed: ${var1}");
    }

    @Override
    StackTraceElement[] getStackTrace() {
        return []
    }

    @Override
    String toString() {
        return ""
    }
}
