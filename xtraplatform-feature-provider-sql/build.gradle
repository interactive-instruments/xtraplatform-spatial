apply plugin: 'scala'

sourceSets {
    String sharedTestDir = project(':xtraplatform-feature-transformer-api').file("src/test/java")
    test {
        java.srcDir sharedTestDir
    }
    main {
        scala {
            outputDir = file("$buildDir/classes/scala/main")
        }
        java {
            outputDir = file("$buildDir/classes/java/main")
        }
    }
    /*main {
        scala {
            srcDirs = ['src/main/scala', 'src/main/java']
        }
        java {
            srcDirs = []
        }
    }*/
}

tasks.compileJava.dependsOn compileScala
tasks.compileScala.dependsOn.remove("compileJava")

dependencies {
    compile files("$sourceSets.main.scala.outputDir")

    provided project(':xtraplatform-geometries')
    provided project(':xtraplatform-crs')
    provided project(':xtraplatform-features')
    provided project(':xtraplatform-feature-provider-api')
    provided project(':xtraplatform-feature-transformer-api')
    provided project(':xtraplatform-geotools')

    //TODO: upgrade
    embedded (group: 'com.lightbend.akka', name: 'akka-stream-alpakka-slick_2.12', version: '2.0.1') {
        exclude  group: 'com.typesafe.akka', module: 'akka-stream_2.12'
        exclude  group: 'org.scala-lang', module: 'scala-library'
        exclude  group: 'com.typesafe', module: 'config'
        exclude  group: 'org.reactivestreams', module: 'reactive-streams'
        exclude  group: 'org.slf4j', module: 'slf4j-api'
    }

    embedded group: 'org.postgresql', name: 'postgresql', version: '42.2.18'

    embedded group: 'org.xerial', name: 'sqlite-jdbc', version: '3.34.0'
    embedded group: 'de.interactive_instruments', name: 'xtraplatform-spatialite', version: '5.0.1-2'

    embedded group: 'us.fatehi', name: 'schemacrawler-api', version: '16.11.6'
    embedded group: 'us.fatehi', name: 'schemacrawler-tools', version: '16.11.6'
    embeddedFlat group: 'us.fatehi', name: 'schemacrawler-postgresql', version: '16.11.6'
    embeddedFlat group: 'us.fatehi', name: 'schemacrawler-sqlite', version: '16.11.6'

    embeddedFlat group: 'com.github.jsqlparser', name: 'jsqlparser', version: '3.2'

    testCompile project(':xtraplatform-feature-transformer-api')
    testCompile group: 'com.typesafe.akka', name: 'akka-testkit_2.12', version: '2.5.31'

    //TODO: gradle 6 fixtures
    testImplementation(project(":xtraplatform-cql"))
    //testImplementation(testFixtures(project(":xtraplatform-cql")))
}

jar {
    manifest {
        instructionFirst 'Import-Package', "!waffle.windows.auth"
        instructionFirst 'Import-Package', "!waffle.windows.auth.impl"
        instructionFirst 'Import-Package', "!org.hibernate"
        instructionFirst 'Import-Package', "!org.hibernate.service"
        instructionFirst 'Import-Package', "!org.hibernate.service.spi"
        instructionFirst 'Import-Package', "!org.hibernate.engine.jdbc.connections.spi"
        instructionFirst 'Import-Package', "!com.sun.jna"
        instructionFirst 'Import-Package', "!com.sun.jna.win32"
        instructionFirst 'Import-Package', "!com.sun.jna.platform.win32"
        instructionFirst 'Import-Package', "!io.prometheus.client"
        instructionFirst 'Import-Package', "!com.sun.jna.ptr"
        instructionFirst 'Import-Package', "!org.osgi.service.jdbc"
        instructionFirst 'Import-Package', "!io.micrometer.core.instrument"
    }
}

// workaround for known issue when using java-library plugin with groovy/scala: https://docs.gradle.org/5.2.1/userguide/java_library_plugin.html#sec:java_library_known_issues
configurations {
    apiElements {
        outgoing.variants.getByName('classes').artifact(
                file: compileScala.destinationDir,
                type: ArtifactTypeDefinition.JVM_CLASS_DIRECTORY,
                builtBy: compileScala)
    }
}
