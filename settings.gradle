pluginManagement {
    repositories {
        gradlePluginPortal()
        maven {
            url "https://dl.interactive-instruments.de/repository/maven-releases/"
        }
        maven {
            url "https://dl.interactive-instruments.de/repository/maven-snapshots/"
        }
    }
}

buildscript {
    configurations.all {
        resolutionStrategy {
            cacheChangingModulesFor 0, 'seconds'
            cacheDynamicVersionsFor 0, 'seconds'
        }
    }
}

plugins {
    id "de.interactive_instruments.xtraplatform-settings" version "6.1.0-SNAPSHOT"
}

def versions =  xtraplatform.getCatalogVersions(file("gradle/layers.versions.toml"))

ext {
    coreVersionCatalog    = versions['xtraplatform-core']
    nativeVersionCatalog  = versions['xtraplatform-native']

    coreVersion    = settings.hasProperty('coreVersionExt') ? settings.getProperty('coreVersionExt') : coreVersionCatalog
    nativeVersion  = settings.hasProperty('nativeVersionExt') ? settings.getProperty('nativeVersionExt') : nativeVersionCatalog
}

xtraplatform {
    includeOnlyIf { layer -> (gradle.parent != null || settings.hasProperty('inc')) && (layer.name != 'xtraplatform-native' || settings.hasProperty('native'))  }

    layerInclude       name: 'xtraplatform-core', group: 'de.interactive_instruments', version: coreVersion, path: '../', dir: 'xtraplatform'
    layerIncludeNative name: 'xtraplatform-native', group: 'de.interactive_instruments', version: nativeVersion, path: '../'
}

dependencyResolutionManagement {
    repositories {
        mavenCentral()
        maven {
            url 'https://jitpack.io'
            //TODO: use version catalog for schemas-ext
            //content { includeGroupAndSubgroups "com.github" }
        }
    }
}

rootProject.name = 'xtraplatform-spatial'
